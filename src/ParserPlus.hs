{-# LANGUAGE PatternSynonyms #-}
{-# LANGUAGE TypeFamilies    #-}
{-# LANGUAGE UnicodeSyntax   #-}
{-# LANGUAGE ViewPatterns    #-}

{- | Utilities for working with parsing, e.g., `Text.Read`, or `Text.Parsec`. -}
module ParserPlus
  ( betweenCs, boundedDoubledChars, braces, brackets, caseInsensitiveChar
  , caseInsensitiveString, digits, doubledChar, doubledChars, many1choice, nl
  , parens, tries, uniquePrefix, utf8BOM, whitespaces

  , tests
  )
where

-- base --------------------------------

import Control.Applicative  ( Alternative, many )
import Control.Monad        ( Monad, liftM2, return, sequence )
import Control.Monad.Fail   ( MonadFail, fail )
import Data.Char            ( toLower, toUpper )
import Data.Either          ( Either( Right ) )
import Data.Eq              ( Eq )
import Data.Function        ( ($) )
import Data.Functor         ( fmap )
import Data.List            ( filter, isPrefixOf, init, inits, tails, zipWith )
import Data.String          ( String )
import Data.Foldable        ( foldl1, toList )
import Data.Traversable     ( Traversable )
import Data.Tuple           ( fst )
import System.Exit          ( ExitCode )
import System.IO            ( IO )

-- base-unicode-symbols ----------------

import Data.Function.Unicode    ( (‚àò) )
import Data.Monoid.Unicode      ( (‚äï) )
import Numeric.Natural.Unicode  ( ‚Ñï )

-- data-textual ------------------------

import Data.Textual  ( Printable, toString )

-- mono-traversable --------------------

import Data.MonoTraversable  ( Element )

-- more-unicode ------------------------

import Data.MoreUnicode.Char         ( ‚ÑÇ )
import Data.MoreUnicode.Applicative  ( (‚ã™), (‚ã´), (‚à§) )
import Data.MoreUnicode.Functor      ( (‚ä≥) )
import Data.MoreUnicode.String       ( ùïä )

-- non-empty-containers ----------------

import NonEmptyContainers.SeqNE             ( (‚ãó), pattern (:‚´∏) )
import NonEmptyContainers.SeqNEConversions  ( ToMonoSeqNonEmpty( toSeqNE ) )

-- parsec ------------------------------

import Text.Parsec.Prim  ( parse )

-- parsers ------------------------------

import Text.Parser.Combinators  ( Parsing, between, choice, count, eof, option
                                , some, try )
import Text.Parser.Char         ( CharParsing, char, digit, noneOf, oneOf )
import Text.Parser.Combinators  ( (<?>), skipOptional )

-- tasty -------------------------------

import Test.Tasty  ( TestTree, testGroup )

-- tasty-hunit -------------------------

import Test.Tasty.HUnit  ( (@=?), testCase )

-- tasty-plus --------------------------

import TastyPlus  ( assertIsLeft, runTestsP, runTestsReplay, runTestTree )

--------------------------------------------------------------------------------

{- | `try` the first thing, then the next thing, until the last thing (which
     isn't surrounded by a `try`) -}

tries ‚à∑ (ToMonoSeqNonEmpty œà, Parsing Œ∑, Element œà ~ Œ∑ Œ±) ‚áí œà ‚Üí Œ∑ Œ±
tries xs = case toSeqNE xs of
             ts :‚´∏ t ‚Üí foldl1 (‚à§) (toList ((try ‚ä≥ ts) ‚ãó t))

----------------------------------------

{- | UTF Byte-Order-Mark, may be seen as the first character of UTF8 files
     https://en.wikipedia.org/wiki/Byte_order_mark -}
utf8BOM ‚à∑ CharParsing Œ∑ ‚áí Œ∑ ‚ÑÇ
utf8BOM = char '\65279'

----------------------------------------

{- | `Text.Parser.Char.spaces` parses *all* spaces, including newline.
     This function skips non-newline whitespaces.
 -}
whitespaces ‚à∑ CharParsing Œ∑ ‚áí Œ∑ String
whitespaces = many $ oneOf " \t"

----------------------------------------

{- | Parse a newline, optionally preceded by a carriage-return.  Because of
     windoze. -}
nl ‚à∑ (CharParsing Œ∑, Monad Œ∑) ‚áí Œ∑ ()
nl = skipOptional (char '\r') ‚ã´ char '\n' ‚ã´ return () <?> "cr/nl"

----------------------------------------

choices ‚à∑ [Œ±] ‚Üí [(Œ±, [Œ±])]
choices xs = zipWith (\ begin (chosen, end) ‚Üí (chosen, begin ‚äï end))
                     -- though init is notionally unsafe, tails always produces
                     -- a non-empty list (tails [] ‚â° [[]]), so init is safe
                     -- in this instance
                     (inits xs) ((\ (a:as) ‚Üí (a,as)) ‚ä≥ init (tails xs))

choicesTests ‚à∑ TestTree
choicesTests =
  testGroup "choices"
      [ testCase "123" $ [(1,[2,3]),(2,[1,3]),(3,[1,2])] @=? choices [1,2,3‚à∑‚Ñï] ]

----------------------------------------

{- | Given a list of parsers, generate a parser that will allow each parser up
     to once in any order.  E.g., many1choice [string "a", string "b"] will
     parse "", "a", "ab", "ba", "b".  We try to be parsimonious about how we do
     that (we don't just generate the cartesian product of possible parses, to
     avoid exponential back-tracking).
  
     Note that you can allow specific repeat counts simply by repeating the
     parser; thus many1choice [string "a", string "a", "string "b"] will parse
     "", "a", "b", "aa", "ab", "ba", "aab", "aba", "baa"
  
     Note also that to avoid backtracking cost, each individual parser is not
     automatically wrapped in a try.  That means that
     many1choice [string "bar", string "baz"]
     will not parse "baz", because the "bar" will consume the "ba" before
     failing.  Thus, if you wish to supply some parsers with common prefices,
     you should wrap those parsers in a try
-}

many1choice ‚à∑ (Monad Œ∑, Alternative Œ∑) ‚áí [Œ∑ Œ±] ‚Üí Œ∑ [Œ±]
many1choice [] = count 0 $ choice []
many1choice xs = option [] ‚àò choice $ many1choice' ‚ä≥ choices xs
-- kept at the top level, rather than in a where, so we get the benefit of
-- memoization
many1choice' ‚à∑ (Monad Œ∑, Alternative Œ∑) ‚áí (Œ∑ Œ±, [Œ∑ Œ±]) ‚Üí Œ∑ [Œ±]
many1choice' (x,xs) = liftM2 (:) x (many1choice xs)

many1choiceTests ‚à∑ TestTree
many1choiceTests =
  let
    aab ‚à∑ (Monad Œ∑, CharParsing Œ∑) ‚áí Œ∑ ùïä
    aab = many1choice [char 'a', char 'a', char 'b'] ‚ã™ eof


    testAAB s = testCase s $ Right s @=? parse aab s s
    testAAB_E s = testCase s $ assertIsLeft (parse aab s s)
   in
    testGroup "many1choice"
              [ testAAB "a"
              , testAAB "b"
              , testAAB "ab"
              , testAAB "ba"
              , testAAB "aa"
              , testAAB "aab"
              , testAAB "aba"
              , testAAB "baa"
              , testAAB "baa"
              , testAAB_E "bb"
              , testAAB_E "bba"
              , testAAB_E "bab"
              , testAAB_E "bba"
              ]

----------------------------------------

{- | Parse between two characters -}
betweenCs ‚à∑ CharParsing Œ∑ ‚áí ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí Œ∑ Œ± ‚Üí Œ∑ Œ±
betweenCs l r = between (char l) (char r)

----------------------------------------

{- | Parse between parentheses -}
parens ‚à∑ CharParsing Œ∑ ‚áí Œ∑ Œ± ‚Üí Œ∑ Œ±
parens = between (char '(') (char ')')

----------------------------------------

{- | Parse between brackets -}
brackets ‚à∑ CharParsing Œ∑ ‚áí Œ∑ Œ± ‚Üí Œ∑ Œ±
brackets = between (char '[') (char ']')

----------------------------------------

{- | Parse between brackets -}
braces ‚à∑ CharParsing Œ∑ ‚áí Œ∑ Œ± ‚Üí Œ∑ Œ±
braces = between (char '{') (char '}')

----------------------------------------

{- | Parse 1 or more digits -}
digits ‚à∑ CharParsing Œ∑ ‚áí Œ∑ ùïä
digits = some digit

----------------------------------------

{- | Parse any character except those in `cs`; they must be doubled.  Thus

     @ parse (many (try $ doubledChar "{}")) "test" "o}}{{p}" ‚â° Right "o}{p" @

     Note the use of `try`; doubleChar will consume the first char of
     non-conformant input.
 -}
doubledChar ‚à∑ CharParsing Œ∑ ‚áí [‚ÑÇ] ‚Üí Œ∑ ‚ÑÇ
doubledChar cs = (choice $ (\ c ‚Üí char c ‚ã´ char c) ‚ä≥ cs) ‚à§ noneOf cs

----------------------------------------

{- | Parse many characters, most directly, but those in `cs` must be doubled up.

     @ parse (doubledChars "{}") "test" "o}}{{p}x" ‚â° Right "o}{p" @
 -}
doubledChars ‚à∑ CharParsing Œ∑ ‚áí [‚ÑÇ] ‚Üí Œ∑ ùïä
doubledChars cs = many (try $ doubledChar cs)

----------------------------------------

{- | Parse many characters, most directly, bounded by `l` on the left and `r`
     on the right; instances of `l` & `r` within the text must be doubled up.

     @ parse (boundedDoubledChars '{' '}') "test" "{o}}{{p}x" ‚â° Right "o}{p" @

     @ parse (boundedDoubledChars '!' '!') "test" "!o}}!!p!" ‚â° Right "o}}!p" @
 -}
boundedDoubledChars ‚à∑ CharParsing Œ∑ ‚áí ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí Œ∑ ùïä
boundedDoubledChars l r = betweenCs l r (doubledChars [l,r])

----------------------------------------

{- | Parse a uniquely matching prefix.

     Given a value table, and a parser; can we parse to something that uniquely
     provides a result?  The parser succeeds if the parse output prefixes
     precisely one result.
 -}
uniquePrefix ‚à∑ (MonadFail Œ∑, Eq Œ±, Printable œá) ‚áí
               [([Œ±],Œ≤)] ‚Üí ([Œ±] ‚Üí œá) ‚Üí Œ∑ [Œ±] ‚Üí Œ∑ Œ≤
uniquePrefix ss e prs = do
  s ‚Üê prs
  case filter ((s `isPrefixOf`) ‚àò fst) ss of
    [(_,y)] ‚Üí return y
    _       ‚Üí fail $ toString (e s)

----------------------------------------

{- | Parse the given character, or the same character in another case
     (upper or lower). -}
caseInsensitiveChar ‚à∑ (Monad Œ∑, CharParsing Œ∑) ‚áí ‚ÑÇ ‚Üí Œ∑ ‚ÑÇ
caseInsensitiveChar c = do
  _ ‚Üê char (toLower c) ‚à§ char (toUpper c)
  return c

--------------------

{- | Parse the given string, but with any combination of upper and lower case
     characters. -}
caseInsensitiveString ‚à∑ (Monad Œ∑, CharParsing Œ∑, Traversable œÜ) ‚áí œÜ ‚ÑÇ ‚Üí Œ∑ (œÜ ‚ÑÇ)
caseInsensitiveString = sequence ‚àò fmap caseInsensitiveChar

--------------------------------------

{-
eChar ‚à∑ Char
eChar = '\\'

escape :: Parser String
escape = pure ‚ä≥ oneOf "\\\"0nrvtbf{}"

nonEscape :: Parser Char
nonEscape = noneOf "\\\"\0\n\r\v\t\b\f{}"

character :: Parser String
character = fmap return nonEscape <|> escape

parseEscaped ‚à∑ String ‚Üí String ‚Üí Parser String
parseEscaped l r = do
    strings <- string l *> many character <* string r
    return $ concat strings
-}

------------------------------------------------------------
--                         tests                          --
------------------------------------------------------------

tests ‚à∑ TestTree
tests = testGroup "ParserPlus" [ choicesTests, many1choiceTests ]

--------------------

_test ‚à∑ IO ExitCode
_test = runTestTree tests

--------------------

_tests ‚à∑ ùïä ‚Üí IO ExitCode
_tests = runTestsP tests

_testr ‚à∑ ùïä ‚Üí ‚Ñï ‚Üí IO ExitCode
_testr = runTestsReplay tests

-- that's all, folks! ----------------------------------------------------------
